// Project description
version = 'dev-alpha'
status = 'indev'
description 'Photon API for plugins'

// Java plugin
apply plugin: 'java'
sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// License plugin
buildscript {
	repositories {
		maven {url 'https://plugins.gradle.org/m2/'}
	}
	dependencies {
		classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1'
	}
}
apply plugin: 'com.github.hierynomus.license'

license {
	header file('HEADER.txt')
	strictCheck true
	mapping('java', 'SLASHSTAR_STYLE')
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
}

// Dependencies
[rootProject, this].each {
    it.repositories {
		mavenCentral()
		maven { url 'https://jitpack.io' }
		maven {url 'https://repo.spacehq.org/content/repositories/releases/'}
    	maven {url 'https://repo.spacehq.org/content/repositories/snapshots/'}
	}
}
dependencies {
	compile 'org.slf4j:slf4j-api:1.7.13+'
	compile 'commons-cli:commons-cli:1.3.1'
	compile 'org.spacehq:mcprotocollib:1.10.2-SNAPSHOT'
}

// Wrapper config
task wrapper(type: Wrapper) {
	gradleVersion = '3.1'
}

